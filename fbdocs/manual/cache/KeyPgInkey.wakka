{{fbdoc item="title" value="INKEY"}}----
Renvoie une chaîne représentant la première touche en attente dans la mémoire tampon du clavier

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Inkey** ( ) [[KeyPgAs as]] [[KeyPgString string]]
##
{{fbdoc item="usage"}}##
	//result// = **Inkey**[$]
##
{{fbdoc item="ret"}}
	Le premier caractère trouvé dans le tampon de clavier ou une chaîne vide (##"####"##) si rien n'est trouvé.

{{fbdoc item="desc"}}
	Vérifie rapidement dans la mémoire tampon du clavier et retourne une représentation ##[[KeyPgString String]]## du premier caractère, s'il y en a un de trouvé. La touche est ensuite retirée de la mémoire tampon et n'est pas envoyée à l'écran. Si la mémoire tampon du clavier est vide, une chaîne vide (##"####"##) est retournée immédiatement, sans attendre d'autres touches.

	Si la valeur est dans le jeu de caractères ASCII, une ##[[KeyPgString String]]## d'un seul caractère correspondant à cette valeur est retournée. Si la touche est une touche "étendue" (pavé numérique, curseur, fonctions) une ##[[KeyPgString String]]## de deux caractères est retournée, dans laquelle le premier représente le caractère étendu (//Voir ci-dessous différences de dialecte//)

	Les touches ""Shift"", ""Ctrl"", ""Alt"" et ""AltGr"" ne peut pas être lues indépendamment par cette fonction car elles n'entrent jamais dans la mémoire tampon du clavier (bien que, de toute évidence, Shift-A sera signalé par ##**Inkey**## différemment de Control-A et ainsi de suite; Alt-A est une touche étendue comme ci-dessus).
	
	Voir aussi ##[[KeyPgInputnum Input()]]## ou ##[[KeyPgGetkey Getkey]]##, ou ##[[KeyPgSleep Sleep]]## pour attendre une touche si la mémoire tampon du clavier est vide.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/input/inkey.bas"}}%%(freebasic)
print "press q to quit"
do
	sleep 1, 1
loop until inkey = "q"
%%
{{fbdoc item="filename" value="examples/manual/input/inkeyext.bas"}}%%(freebasic)
#if __FB_LANG__ = "qb"
#define EXTCHAR Chr$(0)
#else
#define EXTCHAR Chr(255)
#endif

Dim k As String

Print "Press a key, or Escape to end"
Do

	k = Inkey$

	Select Case k

	    Case "A" To "Z", "a" To "z": Print "Letter: " & k
	    Case "1" To "9":             Print "Number: " & k

	    Case Chr$(32): Print "Space"

	    Case Chr$(27): Print "Escape"

	    Case Chr$(32) To Chr$(127)
	        Print "Printable character: " & k

	    Case EXTCHAR & "G": Print "Up Left / Home"
	    Case EXTCHAR & "H": Print "Up"
	    Case EXTCHAR & "I": Print "Up Right / PgUp"

	    Case EXTCHAR & "K": Print "Left"
	    Case EXTCHAR & "L": Print "Center"
	    Case EXTCHAR & "M": Print "Right"

	    Case EXTCHAR & "O": Print "Down Left / End"
	    Case EXTCHAR & "P": Print "Down"
	    Case EXTCHAR & "Q": Print "Down Right / PgDn"

	    Case EXTCHAR & Chr$(59) To EXTCHAR & Chr$(68)
	        Print "Function key: F" & Asc(k, 2) - 58

	    Case EXTCHAR & Chr$(133) To EXTCHAR & Chr$(134)
	        Print "Function key: F" & Asc(k, 2) - 122

	    Case Else
	        If Len(k) = 2 Then
	            Print Using "Extended character: chr$(###, ###)"; Asc(k, 1); Asc(k, 2)
	        ElseIf Len(k) = 1 Then
	            Print Using "Character chr$(###)"; Asc(k)
	        End If

	End Select

	If k = Chr$(27) Then Exit Do

	Sleep 1, 1

Loop
%%

{{fbdoc item="lang"}}
	- Le caractère étendu est ##[[KeyPgChr Chr]](255)## dans les dialectes //[[CompilerOptlang -lang fb]]// et //[[CompilerOptlang -lang fblite]]//.
		- Dans le dialecte //[[CompilerOptlang -lang qb]]//, le caractère étendu dépend de la façon dont le mot-clé est écrit. Si la forme ##**Inkey$**## est utilisée, le caractère étendu est ##[[KeyPgChr Chr]](0)##.  Si elle est référencée comme ##**""__Inkey""**##, le char est étendu ##[[KeyPgChr Chr]](255)##.
		- Dans tous les autres dialectes, le caractère étendu est toujours ##[[KeyPgChr Chr]](255)##.
	- Le suffixe de type chaîne ##$## est facultatif dans les dialectes //[[CompilerOptlang -lang fblite]]// et //[[CompilerOptlang -lang fb]]//, mais nécessaire en dialecte //[[CompilerOptlang -lang qb]]//.

{{fbdoc item="diff"}}
	- Aucune dans le dialecte //[[CompilerOptlang -lang qb]]//.
	- QBasic retourne un ##[[KeyPgChr Chr]](0)## comme premier caractère pour une touche étendue, mais FreeBASIC retourne ##[[KeyPgChr Chr]](255)## comme premier caractère dans les dialectes //[[CompilerOptlang -lang fb]]// et //[[CompilerOptlang -lang fblite]]//.

{{fbdoc item="see"}}
	- ##[[KeyPgSleep Sleep]]##
	- ##[[KeyPgGetkey Getkey]]##
	- ##[[KeyPgInputnum Input()]]##
	- ##[[KeyPgMultikey Multikey]]##

{{fbdoc item="back" value="CatPgInput|User Input"}}

