{{fbdoc item="title" value="PAINT"}}----
Remplit une surface délimitée par une bordure, avec la couleur indiquée

{{fbdoc item="syntax"}}##
	**Paint** [//target//,] [STEP] (//x//, //y//)[, [//paint//][, [//border_color//]]]
##
{{fbdoc item="param"}}
	##//target//##
		le tampon sur lequel on dessine.  
	##STEP##
		indique que les coordonnées sont relatives
	##(//x//, //y//)##
		coordonnées du pixel sur lequel on commence le remplissage en utilisant ##//paint//##
	##//paint//##
		la couleur ou le motif pour effectuer le remplissage
		une valeur numérique indique une couleur, alors qu'une chaîne indique un motif de remplissage
	##//border_color//##
		couleur de la bordure pour limiter le remplissage

{{fbdoc item="desc"}}
	Commande graphique pour remplir une zone délimitée par une bordure d'une certaine couleur. Aussi appelée 'flood-fill' (seau de peinture) ou 'paint bucket' (inondation-remplissage).

	##**Paint**## opère sur la page de travail courante validée par ##[[KeyPgScreenset Screenset]]## ou sur le tampon cible de ##[[KeyPgGetgraphics Get]]##/##[[KeyPgPutgraphics Put]]##, si spécifié.

	Le remplissage commence aux coordonnées ##(//x//,//y//)##; si ##STEP## est précisé, elles sont relatives à la dernière position du curseur graphique. Les coordonnées sont affectées sont affectées par le système de coordonnées personnel établi avec ##[[KeyPgWindow Window]]## et/ou ##[[KeyPgViewgraphics View (Graphics)]]##; le cadrage établi par ##[[KeyPgViewgraphics View]]## s'applique aussi.
	
	Si l'argument ##//paint//## est un nombre, il est utilisé comme une couleur du même format que celui utilisé par ##[[KeyPgColor Color]]## et la région est remplie en utilisant cette couleur. Si ##//paint//## est une ##[[KeyPgString String (chaîne)]]##, la région est remplie avec un motif; le motif a toujours  8 * 8 pixels, et la chaîne passée doit contenir des données dans un format correspondant à la profondeur de couleur courante. La chaîne du motif contient les pixels ligne par ligne, et sa taille doit être comme ci-dessous:

	Pour des profondeurs de couleur de 1, 2, 4 et 8:
	##size = 8 * 8 = 64##
	Pour des profondeurs de couleur de 15 et 16:
	##size = (8 * 8) * 2 = 128##
	Pour des profondeurs de couleur de 24 et 32:
	##size = (8 * 8) * 4 = 256##

	Si la chaîne passée est plus petite, les pixels manquants seront à 0. Si l'argument ##//paint//## est omis, un remplissage normal sera exécuté en utilisant la couleur d'avant-plan validée par ##[[KeyPgColor Color]]##. Le remplissage continue tant que des pixels de la couleur bordure sont trouvés; si ##//border_color//## est omis, la couleur courante d'arrière-plan est utilisée.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/paint.bas"}}%%(freebasic)
' Dessine un cercle blanc interieur peint en bleu
screen 13
circle (160, 100), 30, 15
paint (160, 100), 1, 15
sleep
%%

{{fbdoc item="filename" value="examples/manual/gfx/paint2.bas"}}%%(freebasic)
' Dessine un cercle et le remplit avec un motif a damier

'' Choisit la profondeur de bits pour Screen
'' Essayer de valider d' autres valeurs: 8, 16 ou 32

const bit_depth = 8

'' Fonction pour retourner la couleur pixel, sous forme de chaine
'' retourne la chaine dans le format approprie pour la profondeur de bit courante
function paint_pixel( byval c as uinteger, byval bit_depth_ as integer ) as string
	
	if bit_depth_ <= 8 then '' 8-bit:
	    function =  chr( cubyte(c) )
	    
	elseif bit_depth_ <= 16 then '' 16-bit:
	    function = mkshort( c shr 3 and &h1f or _
	                        c shr 5 and &h7e0 or _
	                        c shr 8 and &hf800 )
	    
	elseif bit_depth_ <= 32 then '' 32-bit:
	    function = mkl(c)
	    
	end if
	
end function


'' Ouvre une fenetre graphique a la profondeur de bit choisie
screenres 320, 200, bit_depth

'' Declare variables pour manier couleurs
dim as uinteger c, c1, c2, cb

'' Declare variable chaine pour manier le motif utilise dans Paint
dim as string paint_pattern = ""

'' Valide couleurs
if bit_depth <= 8 then
	c1 = 7  ''couleur motif 1
	c2 = 8  ''couleur motif 2
	cb = 15 ''couleur bord
else
	c1 = rgb(192, 192, 192) '' couleur motif 1
	c2 = rgb(128, 128, 128) '' couleur motif 2
	cb = rgb(255, 255, 255) '' couleur bord
end if

'' Fabrique le motif a utiliser dans Paint
for y as uinteger = 0 to 7
	for x as uinteger = 0 to 7
	    
	    '' Choisit la couleur du pixel (c)
	    if (x \ 4 + y \ 4) mod 2 > 0 then
	        c = c1
	    else
	        c = c2
	    end if
	    
	    '' Ajoute le pixel au motif
	    paint_pattern = paint_pattern + paint_pixel(c, bit_depth)
	    
	    '' La ligne suivante peut etre utilisee si vous desirez afficher le 
	    '' motif dans le coin haut gauche de l' ecran:
	    
	    ' pset (x, y), c
	    
	next x
next y

'' Dessine un cercle avec la couleur du bord
circle (160, 100), 50, cb, , , 1.0

'' Peint la region du cercle avec paint_pattern, stoppant a la couleur du bord
paint (160, 100), paint_pattern, cb

'' Pause avant fin du programme
sleep
%%

{{fbdoc item="diff"}}
	- ##//target//## est nouveau pour ""FreeBASIC""
	- Dans QB, le motif de remplissage est toujours de 8 bits de largeur et la hauteur est la longueur de la chaîne (jusqu'à 64). Dans FreeBASIC, le motif de remplissage a 8 pixels de large, est indépendent de la profondeur de couleur et la hauteur est toujours de 8.
	- Le paramètre de couleur d'arrière-plan supporté par QB ne l'est pas par la version ""FreeBASIC"".

{{fbdoc item="see"}}
	- ##[[KeyPgScreengraphics Screen]]##

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}

