{{fbdoc item="title" value="Getting Started"}}----
C'est une bonne introduction à FB pour les programmeurs QBasic, sur la base d'un tutoriel de SJ Zero.

==Pour commencer avec le logiciel==

Vous pouvez télécharger FreeBASIC ici: http://www.freebasic.net/index.php/download
Et FBIDE ici: http://fbide.sourceforge.net/

Lors de l'installation de FBIDE, sélectionnez "FBIDE only," pour ne pas installer l'ancienne version de FB incluse dans ce paquet.
Lors de l'exécution FBIDE la première fois, vous aurez à parcourir les répertoires (browse) pour trouver le compilateur FB sur votre ordinateur.

==Hello World!==

Ouvrez FBIDE et tapez la commande suivante:

%%
 PRINT "Bonjour le monde!"
 SLEEP
%%

Maintenant, appuyez sur F5. Félicitations, vous venez de voir comment FreeBASIC est vraiment tout comme QB FreeBASIC. Maintenant, vous pouvez utiliser la plupart des commandes de la console pour QB comme vous vous en souvenez. Par exemple: 

%%
 LOCATE 10,10
 PRINT "Je suis le centre de l'univers!"
 SLEEP
%%

==L'extraordinaire Screen 13==

Maintenant, mettez "SCREEN 13" avant votre code, pour voir combien il est facile d'utiliser les modes graphiques:
%%
 SCREEN 13
 PRINT "Hello World!"
 SLEEP
%%

A partir de là, toutes les commandes graphiques standard fonctionnent, comme vous pouvez le voir dans cet exemple:

%%
 SCREEN 13
 LINE (1,1)-(100,100),1,bf
 PRINT "Hello World!"
 CIRCLE (10,10),10,2
 PSET (30,15),3

 SLEEP
%%

FreeBASIC a également de nouvelles fonctionnalités graphiques. Par exemple, QB n'a jamais eu de SCREEN 14 ou plus. Essayez de lancer ce programme:
%%
 SCREEN 15
 LINE (1,1)-(100,100),1,bf
 PRINT "Hello World!"
 CIRCLE (10,10),10,2
 PSET (30,15),3

 SLEEP
%%

Après l'ouverture d'une fenêtre graphique via la commande SCREEN, vous pouvez également appuyer sur ALT-ENTRER pour changer du mode fenêtré et au mode plein écran.

Une autre fonctionnalité intéressante de la bibliothèque graphique dans FreeBASIC est que vous pouvez faire un changement de page dans n'importe quel mode vidéo. Le code suivant illustre cela. 

%%
 DIM as integer page
 DIM as integer notpage
 DIM as integer a, b


 screen 12, , 2 'This sets the screen for 2 pages
 notpage = 1   'This sets the backpage

 DO
  IF page = 0 THEN page = 1 ELSE page = 0   'These two lines flip the page and the
  IF notpage = 1 THEN notpage = 0 ELSE notpage = 1 'backpage
	
  SCREENSET page, notpage 'This flips the page
	
  CLS  'First we clear the screen
  b = b + 1 
  IF b > 100 THEN b = 0
  FOR a = 1 TO 128
   PSET (b,a),a 'Then we draw a line. It moves without flickering.
  NEXT a
  
 LOOP UNTIL INKEY = CHR(27)
%%

Ceci fonctionne pour n'importe quel mode, vous pouvez donc utiliser les modes à haute résolution pour vos programmes avec le changement de page, en utilisant des commandes graphiques QB! 


==Pourquoi ASM n'est plus nécessaire==

Je n'aurais pas dit cela si ce n'était pas vrai. Utilisation d' ASM dans BASIC pour augmenter la fonctionnalité de votre programme n'est plus nécessaire. Ignorant SDL, Allegro, DirectX, OpenGL, etc... pour une minute, vous avez le changement de page et des modes graphiques avancés à votre disposition, ainsi que Inkey, que nous avons tous appris à aimer ou détester, mais il y a également deux commandes d'entrée nouvelles qui font des choses pour lesquelles les utilisateurs de QB devaient recourir à du code assembleur, depuis la nuit des temps:

%%
 DIM as integer x, y, buttons
 CONST as integer escapeKey = 1
 SCREEN 12

 WHILE NOT MULTIKEY(escapeKey) 'Cela vérifie la touche d'échappement chaque passage
  GETMOUSE x, y, , buttons 'Ceci obtient l'état de la souris 
  PRINT x,y,buttons   
 WEND
%%

Avec cette connaissance, vous devriez être en mesure de commencer la programmation dans FreeBASIC, avec tous les avantages que cela comporte. Vitesse, puissance et la portabilité!
