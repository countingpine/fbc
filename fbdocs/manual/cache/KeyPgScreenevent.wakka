{{fbdoc item="title" value="SCREENEVENT"}}----
Recherche et récupère les événements du système.

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **""ScreenEvent""** ( [[KeyPgByval byval]] event [[KeyPgAs as]] [[KeyPgAny any]] [[KeyPgPtr ptr]] = 0 ) [[KeyPgAs as]] [[KeyPgInteger integer]]
##
{{fbdoc item="usage"}}##
	//result// = **""ScreenEvent""**( [ //event// ] )
##
{{fbdoc item="param"}}
	##//event//##
		Spécifie le tampon où la fonction stockera les données événements.

{{fbdoc item="ret"}}
	Retourne ##-1## s'il existe des événements en attente d'être récupérés, ##0## autrement.

{{fbdoc item="desc"}}
	Cette fonction retourne le dernier événement système disponible dans la file d'attente interne des événements ""GfxLib"". Par "événement" on désigne une activité clavier ou souris quelconque.

	Les données événement (si disponibles) seront copiées dans le tampon pointé par le paramètre "event" pointed et seront déclarées comme un ##[[KeyPgEvent Evénement]]##
	
	__Interrogation des événements__
		La fonction retourne ##-1## s'il y a des événements en attente de récupération, ##0## autrement. Si le paramètre ##//event//## est mis à ##0## (valeur par défaut si omis) ##[[KeyPgScreenevent ScreenEvent]]## ne sera pas en mesure de copier les données d'événement et ne va pas les sortir de la file d'attente interne des événements. L'appel de la fonction de cette façon peut être utile pour vérifier s'il y a des événements disponibles sans les aller chercher.

	__Note__
		Si vous recevez un événement KEY_PRESS, KEY_RELEASE ou KEY_REPEAT, il n'effacera pas le tampon clavier.  Si vous désirez effacer le tampon clavier après réception d'un événement, vous devrez le faire manuellement.  Voir ##[[KeyPgInkey Inkey]]##.

		
{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/screenevent.bas"}}%%(freebasic)
'' include fbgfx.bi pour des definitions utiles
#include "fbgfx.bi"
#if __FB_LANG__ = "fb"
using fb '' constantes et structures sont chargées dans l'espace de nom FB en langue fb
#endif

dim e as EVENT

screenres 640, 480
do
	if (screenevent(@e)) then
		select case e.type
		case EVENT_KEY_PRESS
			if (e.scancode = SC_ESCAPE) then
				end
			end if
			if (e.ascii > 0) then
				print "'" & e.ascii & "'";
			else
				print "unknown key";
			end if
			print " was pressed (scancode " & e.scancode & ")"
		case EVENT_KEY_RELEASE
			if (e.ascii > 0) then
				print "'" & e.ascii & "'";
			else
				print "unknown key";
			end if
			print " was released (scancode " & e.scancode & ")"
		case EVENT_KEY_REPEAT
			if (e.ascii > 0) then
				print "'" & e.ascii & "'";
			else
				print "unknown key";
			end if
			print " is being repeated (scancode " & e.scancode & ")"
		case EVENT_MOUSE_MOVE
			print "mouse moved to " & e.x & "," & e.y & " (delta " & e.dx & "," & e.dy & ")"
		case EVENT_MOUSE_BUTTON_PRESS
			if (e.button = BUTTON_LEFT) then
				print "left";
			elseif (e.button = BUTTON_RIGHT) then
				print "right";
			else
				print "middle";
			end if
			print " button pressed"
		case EVENT_MOUSE_BUTTON_RELEASE
			if (e.button = BUTTON_LEFT) then
				print "left";
			elseif (e.button = BUTTON_RIGHT) then
				print "right";
			else
				print "middle";
			end if
			print " button released"
		Case EVENT_MOUSE_DOUBLE_CLICK
			If (e.button = BUTTON_LEFT) Then
				Print "left";
			Elseif (e.button = BUTTON_RIGHT) Then
				Print "right";
			Else
				Print "middle";
			End If
			Print " button double clicked"
		case EVENT_MOUSE_WHEEL
			print "mouse wheel moved to position " & e.z
		case EVENT_MOUSE_ENTER
			print "mouse moved into program window"
		case EVENT_MOUSE_EXIT
			print "mouse moved out of program window"
		case EVENT_WINDOW_GOT_FOCUS
			print "program window got focus"
		case EVENT_WINDOW_LOST_FOCUS
			print "program window lost focus"
		case EVENT_WINDOW_CLOSE
			end
		end select
	end if

	sleep 1
loop
%%

{{fbdoc item="target"}}
	- ##**ScreenEvent**## ne retourne pas les événements liés à la fenêtre dans la version DOS, mais retourne les événements d'entrée.

{{fbdoc item="lang"}}
	- Non disponible dans le dialecte //[[CompilerOptlang -lang qb]]//.

{{fbdoc item="diff"}}
	- Nouveau pour ""FreeBASIC""

{{fbdoc item="see"}}
	- ##[[KeyPgEvent Event]]##
	- ##[[KeyPgScreengraphics Screen (Graphics)]]##
	- ##[[KeyPgInkey Inkey]]##
	- ##[[KeyPgMultikey Multikey]]##
	- ##[[KeyPgGetmouse Getmouse]]##

{{fbdoc item="back" value="CatPgGfxScreen|Screen Functions"}}