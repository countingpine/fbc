{{fbdoc item="title" value="SUB (Member)"}}----
Déclare ou définit un membre de procédure.

{{fbdoc item="syntax"}}##
	{ [[KeyPgType Type]] | [[KeyPgClass Class]] | [[KeyPgUnion Union]] } //typename//
		[[KeyPgDeclare declare]] [ [[KeyPgStaticMember static]] ] **Sub** //fieldname// [//calling convention specifier//] [ [[KeyPgAlias alias]] //external_name// ] ( [ //parameters// ] ) [ [[KeyPgStatic Static]] ]
	End { [[KeyPgType Type]] | [[KeyPgClass Class]] | [[KeyPgUnion Union]] }

	**Sub** //typename//.//fieldname// ( [ //parameters// ] )
		//statements//
	**End Sub**
##
{{fbdoc item="param"}}
	##//typename//## 
		nom de ##[[KeyPgType Type]]##, ##[[KeyPgClass Class]]##, ou ##[[KeyPgUnion Union]]##
	##//fieldname//## 
		nom de la procédure
	##//external_name//##
		nom du champ tel qu'il est vu quand lié extérieurement
	##//parameters//## 
		les paramètres à passer à la procèdure
	##//spécificateur de convention d'appel//##	
		peut être: ##[[KeyPgCdecl Cdecl]]##, ##[[KeyPgStdcall Stdcall]]## ou ##[[KeyPgPascal Pascal]]##

{{fbdoc item="desc"}}
	Les membres de ##**Sub**## sont accessibles avec ##[[KeyPgOpMemberAccess "Operator . (Accès de membre)"]]## ou ##[[KeyPgOpPtrMemberAccess "Operator -> (Pointeur vers Accès membre)"]]## pour appeler des membres de procédure et peuvent éventuellement accepter des paramètres comme ##[[KeyPgByval byval]]## ou ##[[KeyPgByref byref]]##.  ##//typename//## peut être surchargé sans l'utilisation explicite du mot-clef ##[[KeyPgOverload Overload]]##.

	##//typename//## est le nom du type sous lequel la méthode ##**Sub**## a été déclarée et définie.  La résolution de nom de ##//typename//## suit les mêmes règles que les procèdures quand elles sont utilisées dans un ##[[KeyPgNamespace Namespace]]##.

	Le paramètre caché ##[[KeyPgThis this]]## qui a le même type que ##//typename//## est passé aux membres non-statics des procédures.  ##[[KeyPgThis this]]## est utilisé pour accéder aux champs du ##[[KeyPgType Type]]##, de la ##[[KeyPgClass Class]]##, ou de l'##[[KeyPgUnion Union]]##.

	Un ##[[KeyPgStaticMember Static (Membre)]]## peut être déclaré en utilisant le spécificateur ##**Static**##.  Un ##[[KeyPgConstMember Const (Membre)]]## peut être déclaré en utilisant le spécificateur ##**Const**##.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/udt/sub.bas"}}%%(freebasic)
type Statistics
  count as single
  sum as single
  declare sub AddValue( byval x as single )
  declare sub ShowResults( )
end type

sub Statistics.AddValue( byval x as single )
  count += 1
  sum += x
end sub

sub Statistics.ShowResults( )
  print "Number of Values = "; count
  print "Average          = ";
  if( count > 0 ) then
	print sum / count
  else
	print "N/A"
  end if
end sub

dim stats as Statistics

stats.AddValue 17.5
stats.AddValue 20.1
stats.AddValue 22.3
stats.AddValue 16.9

stats.ShowResults
%%
**Affichage:**
%%
Number of Values =  4
Average          =  19.2
%%

{{fbdoc item="lang"}}
	- Disponible seulement dans le dialecte //[[CompilerOptlang -lang fb]]//.

{{fbdoc item="see"}}
	- ##[[KeyPgClass Class]]##
	- ##[[KeyPgMemberFunction Function (Membre)]]##
	- ##[[KeyPgSub Sub]]##
	- ##[[KeyPgType Type]]##

{{fbdoc item="back" value="CatPgUserDefTypes|User Defined Types"}}