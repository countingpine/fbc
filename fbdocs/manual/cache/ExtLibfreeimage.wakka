{{fbdoc item="title" value="FreeImage"}}----
FreeImage est une bibliothèque Open Source pour les développeurs qui veulent utiliser les formats images classiques tels que PNG, BMP, JPEG, TIFF et d'autres utilisés par les applications multimédia actuelles. FreeImage est facile d'utilisation, rapide, "multi-threading" sécure, compatible avec toutes les versions 32-bit de Windows et multi-plateformes (fonctionne aussi bien avec Linux et Mac OS X).

Site Web: http://freeimage.sf.net
Plates-formes supportées: Win32, Linux
En-têtes à inclure: FreeImage.bi
Exemple inclus: oui, dans examples/gfx/freeimage.bas

Ci-dessous un exemple d'utilisation de FreeImage sous FreeBASIC. Si vous utilisez Windows vous aurez besoin de freeimage.dll qui est disponible   sur le [[http://freeimage.sourceforge.net/ site de FreeImage]].

##%%(freebasic)
'' Code example for loading all common image types using FreeImage.
'' The example loads an image passed as a command line argument.

'' The function FI_Load returns a null pointer (0) if there was an error during
'' loading.  Otherwise it returns a 32-bit PUT compatible buffer.

#include "FreeImage.bi"
#include "crt.bi"
#include "fbgfx.bi"

function FI_Load(filename as string) as any ptr
	if len(filename) = 0 then
		return NULL
	end if

	'' Find out the image format
	dim as FREE_IMAGE_FORMAT form = FreeImage_GetFileType(strptr(filename), 0)
	if form = FIF_UNKNOWN then
	    form = FreeImage_GetFIFFromFilename(strptr(filename))
	end if

	'' Exit if unknown
	if form = FIF_UNKNOWN then
	    return NULL
	end if

	'' Always load jpegs accurately
	dim as uinteger flags = 0
	if form = FIF_JPEG then
	    flags = JPEG_ACCURATE
	end if

	'' Load the image into memory
	dim as FIBITMAP ptr image = FreeImage_Load(form, strptr(filename), flags)
	if image = NULL then
	    '' FreeImage failed to read in the image
	    return NULL
	end if

	'' Flip the image so it matches FB's coordinate system
	FreeImage_FlipVertical(image)

	'' Convert to 32 bits per pixel
	dim as FIBITMAP ptr image32 = FreeImage_ConvertTo32Bits(image)

	'' Get the image's size
	dim as uinteger w = FreeImage_GetWidth(image)
	dim as uinteger h = FreeImage_GetHeight(image)

	'' Create an FB image of the same size
	dim as fb.Image ptr sprite = imagecreate(w, h)

	dim as byte ptr target = cptr(byte ptr, sprite + 1)
	dim as integer target_pitch = sprite->pitch

	dim as any ptr source = FreeImage_GetBits(image32)
	dim as integer source_pitch = FreeImage_GetPitch(image32)

	'' And copy over the pixels, row by row
	for y as integer = 0 to (h - 1)
	    memcpy(target + (y * target_pitch), _
	           source + (y * source_pitch), _
	           w * 4)
	next

	FreeImage_Unload(image32)
	FreeImage_Unload(image)

	return sprite
end function

screenres 640, 480, 32

dim as string filename = command(1)

dim as any ptr image = FI_Load(filename)
if image <> 0 then
	put (0, 0), image
else
	print "Problem while loading file : " & filename
end if

sleep
%%##

{{fbdoc item="back" value="ExtLibTOC|External Library Table of Contents"}}