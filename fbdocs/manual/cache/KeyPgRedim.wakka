{{fbdoc item="title" value="REDIM"}}----
Définit ou redimensionne un tableau de longueur-variable

{{fbdoc item="syntax"}}##
	**Redim** [ [[KeyPgShared Shared]] ] [ [[KeyPgPreserve Preserve]] ] //symbolname//([//subscript// [, ...]]) As [[DataType datatype]] [, ...]
	**Redim** [ [[KeyPgShared Shared]] ] [ [[KeyPgPreserve Preserve]] ] As [[DataType datatype]] //symbolname//([//subscript// [, ...]]) [, ...]
##
{{fbdoc item="param"}}
	##[[KeyPgShared Shared]]##
		Spécifie un accès partagé au tableau (pour la portée du fichier) à travers le module.
	##[[KeyPgPreserve Preserve]]##
		Lors de l'utilisation avec un tableau existant, le contenu de ce tableau sera préservé pendant le redimensinnement. Notez que ##[[KeyPgPreserve Preserve]]## ne fonctionnera pas correctement dans tous les cas.
	##//symbolname//##
		Un identificateur (id) de tableau, nouveau ou existant déjà.
	##//subscript//##: [ ##//lowerbound// To## ] ##//upperbound//##
		Les limites inférieure et supérieure pour une dimension du tableau. La limite inférieure à zéro (##0##) par défaut ou la ##[[KeyPgOptionbase Base]]## par défaut, si non spécifié.
	##[[DataType datatype]]##
		Le type des éléments contenus dans le tableau.

{{fbdoc item="desc"}}
	##**ReDim**## peut être utilisé pour définir de nouveaux tableaux de longueur variable ou pour redimensionner des tableaux de longueur variable. ##**ReDim**## produit toujours des tableaux de longueur variable, ainsi, contrairement à ##[[KeyPgDim Dim]]##, des tableaux de longueur variable peuvent être définis avec des indices constants.

	Lorsque vous définissez un nouveau tableau de longueur variable, ses éléments sont construits par défaut. Pour des types de données simples comme ##[[KeyPgInteger Integer]]## ou ##[[KeyPgDouble Double]]##, les éléments sont initialisés à zéro (##0##). Pour des types définis par l'utilisateur avec un constructeur par défaut, celui-ci sera appelé.

	**NOTES**: 
	- ##**Redim** [[KeyPgPreserve Preserve]]## ne donnera pas le résultat escompté dans les cas suivants:
		Le comportement normal de ##[[KeyPgPreserve Preserve]]## est de conserver les données originales contiguës dans la mémoire, et seulement augmenter ou diminuer la taille de la mémoire.
		Son comportement (avec une seule dimension) n'est bien défini que lorsque la borne supérieure est changée. Si la borne inférieure est modifiée, le résultat est que les données sont en effet déplacées pour commencer à la nouvelle limite inférieure.
		Avec des dimensions multiples, seule la limite supérieure de la première dimension peut être augmentée en toute sécurité. Si la première dimension est réduite, les données existantes cartographiables peuvent être perdues. Si les dimensions d'ordre inférieur sont toutes redimensionnées, les effets sont imprévisibles.

	- ##**Redim**## ne peut pas être utilisé sur des tableaux de taille fixe - c'est à dire avec des tableaux avec des limites constantes produits par ##[[KeyPgDim Dim]]##.  Ceci inclut les tableaux contenus dans les ##[[KeyPgType Type]]##s définis par l'utilisateur, car seuls des tableaux à taille fixée sont supportés par les types définis par l'utilisateur.  Ceci inclut aussi les tableaux de longueur fixée passés comme paramètres d'une fonction.  ""FreeBASIC"" ne pourra vous empêcher d'essayer cela au moment de la compilation, mais les résultats au moment du fonctionnement seront imprévisibles.

	- L'utilisation de ##**Redim**## dans un membre d'une procédure avec un tableau qui contient une instance de la classe objet est indéfinie et entraînera d'horribles crashes.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/array/redim.bas"}}%%(freebasic)
'' Define a variable-length array with 5 elements
''
ReDim array(0 To 4) As Integer

For index As Integer = LBound(array) To UBound(array)
	array(index) = index
Next

'' Resize a variable-length array with 10 elements 
'' (the lower bound should be kept the same)
ReDim Preserve array(0 to 9) As Integer

Print "index", "value"
For index As Integer = LBound(array) To UBound(array)
	Print index, array(index)
Next
%%
	Ce programme produira la sortie suivante:

%%index         value
 0             0
 1             1
 2             2
 3             3
 4             4
 5             5
 6             0
 7             0
 8             0
 9             0
%%

{{fbdoc item="filename" value="examples/manual/array/redim2d.bas"}}%%(freebasic)
'' Define a variable-length array
Dim array() As Integer

'' ReDim array to have 3*4 elements
ReDim array(1 To 3, 1 To 4)

Dim As Integer n = 1, i, j

Print "3 * 4:"
Print
For i = LBound(array, 1) To UBound(array, 1)
	For j = LBound(array, 2) To UBound(array, 2)
	    array(i, j) = n
	    Print Using "##  "; array(i, j);
	    n += 1
	Next
	Print
Next
Print


'' ReDim Preserve array to have 4*4 elements, preserving the contents
'' (only the first upper bound should be changed)
ReDim Preserve array(1 To 4, 1 To 4) As Integer

Print "4 * 4:"
Print
For i = LBound(array, 1) To UBound(array, 1)
	For j = LBound(array, 2) To UBound(array, 2)
	    Print Using "##  "; array(i, j);
	Next
	Print
Next
Print


'' ReDim Preserve array to have 2*4 elements, preserving but trancating the contents
'' (only the first upper bound should be changed)
ReDim Preserve array(1 To 2, 1 To 4) As Integer

Print "2 * 4:"
Print
For i = LBound(array, 1) To UBound(array, 1)
	For j = LBound(array, 2) To UBound(array, 2)
	    Print Using "##  "; array(i, j);
	Next
	Print
Next
Print
%%
	Ce programme produira la sortie suivante:

%%3 * 4:

 1   2   3   4
 5   6   7   8
 9  10  11  12

4 * 4:

 1   2   3   4
 5   6   7   8
 9  10  11  12
 0   0   0   0

2 * 4:

 1   2   3   4
 5   6   7   8

%%
{{fbdoc item="diff"}}
	- ##[[KeyPgPreserve Preserve]]## existe dans Visual Basic, mais pas dans QBASIC.
	- Les tableaux multi-dimensionnels de ""FreeBASIC"" sont d'ordre ligne-majeur, plutôt que d'ordre colonne-majeur.

{{fbdoc item="see"}}
	- ##[[KeyPgCommon Common]]##
	- ##[[KeyPgDim Dim]]##
	- ##[[KeyPgErase Erase]]##
	- ##[[KeyPgExtern Extern]]##
	- ##[[KeyPgLbound Lbound]]##
	- ##[[KeyPgRedim Redim]]##
	- ##[[KeyPgPreserve Preserve]]##
	- ##[[KeyPgShared Shared]]##
	- ##[[KeyPgStatic Static]]##
	- ##[[KeyPgUbound Ubound]]##
	- ##[[KeyPgVar Var]]##

{{fbdoc item="back" value="CatPgArray|Array Functions"}}

