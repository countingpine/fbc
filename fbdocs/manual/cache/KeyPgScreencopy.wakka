{{fbdoc item="title" value="SCREENCOPY"}}----
Copie le contenu d'une page graphique dans une autre page graphique

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgSub sub]] **Screencopy** ( [[KeyPgByval byval]] //frompage// [[KeyPgAs as]] [[KeyPgInteger integer]] = -1, [[KeyPgByval byval]] //topage// [[KeyPgAs as]] [[KeyPgInteger integer]] = -1 )
##
{{fbdoc item="usage"}}##
	**Screencopy** [ //from_page// ] [, //to_page// ]
##
{{fbdoc item="param"}}
	##//frompage//##
		page à copier
	##//topage//##
		page vers laquelle copier

{{fbdoc item="desc"}}
	##//from_page//## est la page que l'on copie. Si cet argument est omis, la page de travail courante est utilisée.

	##//to_page//## est la page vers laquelle on copie. Si cet argument est omis, la page visible courante est utilisée.

	Les numéros de page varient de 0 à num_pages spécifié lors du réglage du mode graphique moins 1 

	Vous pouvez utiliser cette fonction pour ajouter un double tampon à vos graphiques. Tous les modes graphiques supportent cette fonction; vous devez fournir des numéros de page valides sinon ##**ScreenCopy**## n'aura aucun effet.

	Il existe deux autres fonctions similaires: ##[[KeyPgFlip Flip]]## et ##[[KeyPgPcopy PCopy]]##.  ##[[KeyPgFlip Flip]]## est conçu pour fonctionner en mode ""OpenGL"", alors que ##[[KeyPgPcopy PCopy]]## supporte les pages console sur certaines plates-formes.  Les deux font la même chose que ##**ScreenCopy**## dans les modes graphiques normaux.

Exemple:
	Voir l'exemple de ##[[KeyPgScreenset Screenset]]##.


{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/screencopy.bas"}}%%(freebasic)
'' 320x200x8, avec 3 pages
screen 13,,3

'' image pour page #1
screenset 1, 0
cls
circle( 160, 100 ), 90, 1 ,,,, f
circle( 160, 100 ), 90, 15
print "Press 2 to copy page #2 to visible page"
print "Press escape to exit"

'' image pour page #2
screenset 2, 0
cls
line( 50, 50 )-( 270, 150 ), 2, bf
line( 50, 50 )-( 270, 150 ), 15, b
print "Press 1 to copy page #1 to visible page"
print "Press escape to exit"

'' page #0 est la page visible
screenset 0, 0
cls
print "Press 1 to copy page #1 to visible page"
print "Press 2 to copy page #2 to visible page"
print "Press escape to exit"

dim k as string

do
  k = inkey
  select case k
  case chr(27)
	exit do
  case "1"
	screencopy 1, 0
  case "2"
	screencopy 2, 0
  end select

  sleep 25
loop
%%


{{fbdoc item="lang"}}
	- Non disponible dans le dialecte //[[CompilerOptlang -lang qb]]// sauf si référencé avec l'alias ##**""__Screencopy""**##.

{{fbdoc item="diff"}}
	- Nouveau pour ""FreeBASIC"". QB a ##[[KeyPgPcopy Pcopy]]## comme fonction équivalente.

{{fbdoc item="see"}}
	- ##[[KeyPgScreengraphics Screen (Graphics)]]##
	- ##[[KeyPgScreenres Screenres]]##
	- ##[[KeyPgScreenset Screenset]]##

{{fbdoc item="back" value="CatPgGfxScreen|Screen Functions"}}

