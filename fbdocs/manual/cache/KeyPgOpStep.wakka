{{fbdoc item="title" value="Operator Step (Iteration)"}}----
Incrémente l'itérateur d'une boucle ##[[KeyPgFornext For...Next]]##

{{fbdoc item="syntax"}}##
	{ [[KeyPgType Type]] | [[KeyPgClass Class]] | [[KeyPgUnion Union]] } //typename//
		[[KeyPgDeclare declare]] [[KeyPgOperator operator]] **Step** ()
		[[KeyPgDeclare declare]] [[KeyPgOperator operator]] **Step** ( [ [[KeyPgByref byref]] | [[KeyPgByval byval]] ] //stp// [[KeyPgAs as]] //typename// )
		//...//
	End { [[KeyPgType Type]] | [[KeyPgClass Class]] | [[KeyPgUnion Union]] }
##
{{fbdoc item="usage"}}##
	[[KeyPgFornext For]] //iterator// [ As //typename// ] = //start_value// To //end_value// [ [[KeyPgFornext Step]] //step_value// ]
		[// ...statements... //]
	[[KeyPgFornext Next]]
##
{{fbdoc item="param"}}
	##//typename//##
		nom du ##[[KeyPgType Type]]##, de la ##[[KeyPgClass Class]]## ou de l'##[[KeyPgUnion Union]]##
	##//stp//##, ##//step_value//##
		un objet ##//typename//## utilisé comme valeur d'incrémentation
	##//iterator//##
		un objet ##//typename//## utilisé comme itérateur
	##//end_value//##
		un objet ##//typename//## utilisé comme valeur de fin de boucle
	##//start_value//##
		un objet ##//typename//## utilisé pour la construction de copie ou pour initialiser l'itérateur

{{fbdoc item="desc"}}
	##[[KeyPgOpFor Operator For]]##, ##[[KeyPgOpNext Operator Next]]## et ##**Operator Step**## peut être surchargé dans des définitions de type utilisateur pour permettre à des objets de ce type d'être utilisés comme itérateurs et valeurs d'incrémentation dans des boucles ##[[KeyPgFornext For...Next]]##.

	L'##**Operator Step**## est appelé pour incrémenter l'itérateur immédiatement après l'exécution de toutes les déclarations, s'il y en a, de la boucle ##[[KeyPgFornext For...Next]]##.

	La première version d'##**Operator Step**## est utilisée si aucune valeur d'incrémentation n'est donnée dans la déclaration ##[[KeyPgFornext For...Next]]##. Si la valeur d'incrémentation est donnée, la seconde version est utilisée et passée.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/udt/step.bas"}}%%(freebasic)
'' Example Type
Type T
  '' value is set by the constructor
  value As Double
  Declare Constructor( ByVal x As Double = 0 )

  Declare Operator For( ByRef stp As T )
  Declare Operator Step( ByRef stp As T )
  Declare Operator Next( ByRef cond As T, ByRef stp As T ) As Integer
End Type

Constructor T ( ByVal x As Double )
  print "T iterator constructed with value " & x
  value = x
End Constructor

Operator T.for( ByRef stp As T )
End Operator

Operator T.step( ByRef stp As T )
  print " incremented by " & stp.value & " in step."
  value += stp.value
End Operator

Operator T.next( ByRef cond As T, ByRef stp As T ) As Integer
  '' iterator's moving from a high value to a low value (step >= 0)
  If( stp.value < 0 ) Then
	return( value >= cond.value )
  Else
  '' iterator's moving from a low value to a high value (step < 0)
	return( value <= cond.value )
  End If
End Operator

'' Example Usage. It looks like we are working with numbers, but the iterators
'' have overloaded constructors. The 10, 1, and -1 are all of type T.
For i As T = 10 To 1 Step -1
  Print i.value;
Next i
%%
Un exemple plus pratique démontrant l'itération de fichier basé sur [[http://www.freebasic.net/forum/viewtopic.php?p=83564 cha0s' file iteration class]].
{{fbdoc item="filename" value="examples/manual/udt/stepFileIteration.bas"}}%%(freebasic)
'' a class which iterates through files
type FileIter
	as string pathName, fileName
	declare constructor( byref pathName as string )

	declare operator for()
	declare operator step()
	declare operator next( byref endCond as FileIter) as integer
end type

Constructor FileIter( Byref pathName As String )   
	this.pathName = pathName
End Constructor

Operator FileIter.for( )   
	fileName = dir(pathName & "/*.*")   
End Operator

Operator FileIter.step( )   
	fileName = dir("")
End Operator

Operator FileIter.next( Byref endCond As FileIter ) As Integer
	Return(fileName <> endCond.pathName)   
	'' the c'tor sets the path name and so we check against that
End Operator

'' example code
'' change it to any directory
For i As FileIter = "./" To ""
	print i.fileName
Next
%%
{{fbdoc item="lang"}}
	- Disponible seulement dans le dialecte //[[CompilerOptlang -lang fb]]//.

{{fbdoc item="see"}}
	- ##[[KeyPgOpFor Operator For]]##
	- ##[[KeyPgOpNext Operator Next]]##
	- ##[[KeyPgFornext For...Next]]##

{{fbdoc item="back" value="CatPgOpIterating|Iterating Operators"}}{{fbdoc item="back" value="CatPgOperators|Operators"}}