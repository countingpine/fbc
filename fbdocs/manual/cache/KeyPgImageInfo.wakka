{{fbdoc item="title" value="IMAGEINFO"}}----
Récupère les informations sur une image

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Imageinfo** ( [[KeyPgByval byval]] //image// [[KeyPgAs as]] [[KeyPgAny any]] [[KeyPgPtr ptr]], [[KeyPgByref byref]] //width// [[KeyPgAs as]] [[KeyPgInteger integer]] = 0, [[KeyPgByref byref]] //height// [[KeyPgAs as]] [[KeyPgInteger integer]] = 0, [[KeyPgByref byref]] //bypp// [[KeyPgAs as]] [[KeyPgInteger integer]] = 0, [[KeyPgByref byref]] //pitch// [[KeyPgAs as]] [[KeyPgInteger integer]] = 0, [[KeyPgByref byref]] //pixdata// [[KeyPgAs as]] [[KeyPgAny any]] [[KeyPgPtr ptr]] = 0, [[KeyPgByref byref]] //size// [[KeyPgAs as]] [[KeyPgInteger integer]] = 0 ) [[KeyPgAs as]] [[KeyPgInteger integer]]
##
{{fbdoc item="usage"}}##
	//result// = **Imageinfo**( //image// [, [//width//] [, [//height//] [, [//bypp//] [, [//pitch//] [, [//pixdata//] [, //size//]]]]]] )
##
{{fbdoc item="param"}}
	##//image//##
		L'addresse de l'image.
	##//width//##
		Stocke la largeur de l'image, en pixels.
	##//height//##
		Stocke la hauteur de l'image, en pixels.
	##//bypp//##
		Stocke le nombre d'octets par pixel de l'image - c.a.d. la taille d'un seul pixel, en octets.
	##//pitch//##
		Stocke le "pitch" de l'image - c.a.d. la taille de chaque ligne (row), en octets.
		Notez que ceci est meilleur que simplement ##//width (largeur)// * //bypp//##, car les lignes peuvent être modifiées pour un meilleur alignement en mémoire.
	##//pixdata//##
		Stocke l'adresse de début de la première ligne de l'image.
	##//size//##
		Stocke la taille de la mémoire image, en octets.

{{fbdoc item="section" value="Valeur Retournée"}}
	Si ##//image//## ne pointe pas sur une image valide, un (##1##) est retourné. Sinon, ##//width//##, ##//height//##, ##//bypp//##, ##//pitch//##, ##//pixdata//## et ##//size//## reçoivent les valeurs appropriées, et zéro (##0##) est retourné.

{{fbdoc item="desc"}}
	##**ImageInfo**## procure des informations variées sur une image, comme ses dimensions et sa profondeur de couleur, mais aussi les informations nécessaires pour accéder directement à toutes les données pixel du tampon.

	##**ImageInfo**## procure aussi la taille de l'image en mémoire, ce qui est utile pour l'allocation de mémoire pour copier l'image existante, ou pour écrire l'image dans un fichier.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/imageinfo.bas"}}%%(freebasic)
'' Cree ecran graphique 32-bit et image.
screenres 320, 200, 32
dim image as any ptr = imagecreate( 64, 64 )

dim pitch as integer
dim pixels as any ptr

'' Obtient assez d'informations pour se deplacer dans donnees pixel.
if 0 <> imageinfo( image, ,,, pitch, pixels ) then
	print "unable to retrieve image information."
	sleep
	end
end if

'' Dessine un motif sur l'image en manipulant directement pixel en memoire.
for y as integer = 0 to 63
	dim row as uinteger ptr = pixels + y * pitch

	for x as integer = 0 to 63
		row[x] = rgb(x * 4, y * 4, (x xor y) * 4)
	next x
next y

'' Dessine l'image sur l'ecran.
put (10, 10), image

imagedestroy( image )

sleep
%%
{{image class="center" title="Output image for ImageInfo example" url="/images/imageinfo.png"}}


{{fbdoc item="lang"}}
	- Non accessible en dialecte //[[CompilerOptlang -lang qb]]// saut si référencé avec l'alias ##**__Imageinfo**##.

{{fbdoc item="diff"}}
	- Nouveau pour ""FreeBASIC""

{{fbdoc item="see"}}
	- ##[[KeyPgImagecreate ImageCreate]]##
	- ##[[KeyPgImageDestroy ImageDestroy]]##
	- ##[[KeyPgImageConvertRow ImageConvertRow]]##
	- ##[[KeyPgGetgraphics Get (Graphique)]]##
	- ##[[KeyPgPutgraphics Put (Graphique)]]##
	- [[GfxInternalFormats Formats graphiques internes]]

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}

