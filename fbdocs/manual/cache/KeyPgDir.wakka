{{fbdoc item="title" value="DIR"}}----
Recherche et renvoie des informations sur un &eacute;l&eacute;ment dans le syst&egrave;me de fichiers, effectue une recherche dans les r&eacute;pertoire.

{{fbdoc item="syntax"}}##
	# [[KeyPgInclude include]] "dir.bi"

	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Dir** [[KeyPgOverload overload]] ( [[KeyPgByref byref]] //item_spec// [[KeyPgAs as]] [[KeyPgConstQualifier const]] [[KeyPgString string]], [[KeyPgByval byval]] //attrib_mask// [[KeyPgAs as]] [[KeyPgInteger integer]] = **fbNormal**, [[KeyPgByref byref]] //out_attrib// [[KeyPgAs as]] [[KeyPgInteger integer]] ) [[KeyPgAs as]] [[KeyPgString string]]
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Dir** ( [[KeyPgByref byref]] //item_spec// [[KeyPgAs as]] [[KeyPgConstQualifier const]] [[KeyPgString string]], [[KeyPgByval byval]] //attrib_mask// [[KeyPgAs as]] [[KeyPgInteger integer]] = **fbNormal**, [[KeyPgByval byval]] //p_out_attrib// [[KeyPgAs as]] [[KeyPgInteger integer]] [[KeyPgPtr ptr]] = 0 ) [[KeyPgAs as]] [[KeyPgString string]]
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Dir** ( [[KeyPgByval byval]] //attrib_mask// [[KeyPgAs as]] [[KeyPgInteger integer]] = **fbNormal**, [[KeyPgByref byref]] //out_attrib// [[KeyPgAs as]] [[KeyPgInteger integer]] ) [[KeyPgAs as]] [[KeyPgString string]]
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Dir** ( [[KeyPgByval byval]] //attrib_mask// [[KeyPgAs as]] [[KeyPgInteger integer]] = **fbNormal**, [[KeyPgByval byval]] //p_out_attrib// [[KeyPgAs as]] [[KeyPgInteger integer]] [[KeyPgPtr ptr]] = 0 ) [[KeyPgAs as]] [[KeyPgString string]]
##
{{fbdoc item="usage"}}##
	//result// = **Dir**( //item_spec//, [ //attrib_mask// ], //out_attrib// ] )
	//result// = **Dir**( //item_spec// [, [ //attrib_mask// ] [, //p_out_attrib// ] ] )
	//result// = **Dir**( //out_attrib// )
	//result// = **Dir**( [ //p_out_attrib// ] )
##
{{fbdoc item="param"}}
	##//item_spec//##
		Le mod&egrave;le correspondant au nom d'un &eacute;l&eacute;ment.
	##//attrib_mask//##
		Le masque de bits correspondant aux attributs d'un &eacute;l&eacute;ment.
	##//out_attrib//##
		R&eacute;f&eacute;rence &agrave; un masque de bits qui est assign&eacute; &agrave; chacun des attributs de l'objet trouv&eacute;, le cas &eacute;ch&eacute;ant.
	##//p_out_attrib//##
		Pointeur vers un masque de bits qui est assign&eacute; &agrave; chacun des attributs de l'objet trouv&eacute;, le cas &eacute;ch&eacute;ant.

{{fbdoc item="section" value="Valeur retourn&eacute;e
	Si aucun item correspondant au nom ##//item_spec//## ou au masque d'attribut ##//attrib_mask//## nest trouv&eacute;, alors ##//out_attrib//## (ou ##*//p_out_attrib//##) re&ccedil;oit la valeur z&eacute;ro et ue cha&icirc;ne vide est retourn&eacute;e. Autrement, ##//out_attrib//## (ou ##*//p_out_attrib//##) re&ccedil;oit le masque d'attribut de l'item, et le nom de l'item, sans chemin, est retourn&eacute;.

{{fbdoc item="desc"}}
	Si ##//item_spec//## contient un chemin absolu, alors la premi&egrave;re proc&eacute;dure recherche dans le syst&egrave;me de fichiers un item qui correspond au nom ##//item_spec//## et dont tous les attributs sont tous contenus dans ##//attrib_mask//##. Autrement, il recherche par rapport au r&eacute;pertoire courant (voir ##[[KeyPgCurdir CurDir]]##). En tout cas, si aucun &eacute;l&eacute;ment correspondant n'est trouv&eacute;, z&eacute;ro est attribu&eacute; &agrave; ##//out_attrib//## et une cha&icirc;ne vide est retourn&eacute;e. Sinon, ##//out_attrib//## est affect&eacute; avec les indicateurs d'attribut de l'&eacute;l&eacute;ment, et le nom de l'&eacute;l&eacute;ment, sans chemin d'acc&egrave;s, est retourn&eacute;.

	##//item_spec//## peut inclure un ast&eacute;risque (##*##, pour correspondre &agrave; des caract&egrave;res adjacents) ou un ou plusieurs points d'interrogation  (##?##, pour correspondre &agrave; tout caract&egrave;re individuel). Si c'est le cas, la proc&eacute;dure recherche le premier item du genre. S'il est trouv&eacute;, les appels suivants avec ##//item_spec//## &eacute;gal &agrave; une cha&icirc;ne vide retournera l'&eacute;l&eacute;ment suivant correspondant au nom ##//item_spec//## jusqu'&agrave; ce qu'aucun autre item correspondant ne soit trouv&eacute;. Si ##//attrib_mask//## est omis de ces appels ult&eacute;rieurs, la proc&eacute;dure recherche des items avec les m&ecirc;mes attributs que lors de l'appel pr&eacute;c&eacute;dent.

	La seconde proc&eacute;dure a le m&ecirc;me comportement que ##**Dir**( //item_spec//, //attrib_mask//, *//p_out_attrib// )##.
	
	La troisi&egrave;me proc&eacute;dure a le m&ecirc;me comportement que ##**Dir**( "####", , //out_attrib// )##.

	La quatri&egrave;me proc&eacute;dure a le m&ecirc;me comportement que ##**Dir**( "####", , *//p_out_attrib// )##.

**Attributs des fichiers:**
	Les fichiers, r&eacute;pertoires et autres items, si on peut dire, poss&egrave;dent des sortes d'attributs de fichier; les m&eacute;tadonn&eacute;es qui d&eacute;crivent l'item. Le sens de ces m&eacute;tadonn&eacute;es varie en fonction du syst&egrave;me d'exploitation et du syst&egrave;me utilis&eacute;. Les macros suivantes sont utilis&eacute;es comme "bit-flags" avec ##//attrib_mask//##, ##//out_attrib//## et ##*//p_out_attrib//##. Leurs valeurs peuvent &ecirc;tre combin&eacute;es pour former un masque en utilisant l'##[[KeyPgOpOr Op&eacute;rateur Or]]##. Pour y acc&eacute;der, ##[[KeyPgInclude Include]] "dir.bi"##.

	Les items trouv&eacute;s sans attributs correspondent toujours, ind&eacute;pendamment de la valeur de ##//attrib_mask//##. Un item ne correspondra pas s'il a un ou plusieurs attributs qui ne sont pas sp&eacute;cifi&eacute;s dans ##//attrib_mask//##. De plus, si ##//attrib_mask// [[KeyPgOpAnd And]] **fbArchive**## &eacute;gale z&eacute;ro, alors ##//attrib_mask//## est attribu&eacute; &agrave; ##//attrib_mask// [[KeyPgOpOr Or]] **fbDirectory**## avant de v&eacute;rifier si les attributs du fichier correspondent.
		## # define fbReadOnly &h01 ##
		//L'item ne peut &ecirc;tre &eacute;crit ou supprim&eacute;.//
		**DOS & Windows**: L'item a l'attribut "read-only" (lecture seule) valid&eacute;.

	**Linux**:L'item n'a pas de permission d'&eacute;criture associ&eacute;e avec l'utilisateur ou le groupe courant, il n'est pas accessible en &eacute;criture au niveau global.  (Que l'utilisateur ait ou non les permissions "root" n'a aucune importance.)
		## # define fbHidden &h02 ##
		//L'item est cach&eacute; dans dans une liste normale de r&eacute;pertoire.//
		**DOS & Windows**: L'item a l'attribut "hidden" (cach&eacute;) valid&eacute;.

	**Linux**: Le nom de l'item a un point (##.##) comme premier caract&egrave;re.
		## # define fbSystem &h04 ##
		//L'item est utilis&eacute; presque exclusivement par le syst&egrave;me.//
		**DOS & Windows**: L'item a l'attribut "system" (syst&egrave;me) valid&eacute;.

	**Linux**: L'item est soit un p&eacute;riph&eacute;rique en mode caract&egrave;re, un p&eacute;riph&eacute;rique en mode bloc, un tube nomm&eacute; (FIFO) ou un socket Unix.
		## # define fbDirectory &h10 ## 
		//L'item est un r&eacute;pertoire. Inclus les r&eacute;pertoire courant (##.##) et parent (##..##).//

	**DOS & Windows**: L'item a l'attribut "directory" (r&eacute;pertoire) valid&eacute;.
		**Linux**: L'item est un r&eacute;pertoire.
		## # define fbArchive &h20 ##
		//L'item peut &ecirc;tre sauvegard&eacute; apr&egrave;s quelque op&eacute;ration automatis&eacute;e.//

	**DOS & Windows**: L'item a l'attribut "archive" valid&eacute;.
		**Linux**: L'item n'est pas un r&eacute;pertoire; les syst&egrave;mes de fichiers typiques ne supportent pas ces m&eacute;tadonn&eacute;es.

	## # define fbNormal (fbReadOnly ou fbArchive) ##
	//L'item est un fichier ou un r&eacute;pertoire.//

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/system/dir.bas"}}%%(freebasic)
#include "dir.bi" 'provides constants to use for the attrib_mask parameter

Sub list_files (byref filespec as string, byval attrib as integer)
	Dim filename As String

	filename = dir( filespec, attrib )
	Do
	    Print filename
	    filename = dir( )
	Loop While len( filename ) > 0
End Sub

Print "directories:"
list_files "*", fbDirectory

Print
Print "archive files:"
list_files "*", fbArchive
%%

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/system/dirretat.bas"}}%%(freebasic)
'' Example of using DIR function and retrieving attributes

#include "dir.bi" '' provides constants to match the attributes against

'' set input attribute mask to allow files that are normal, hidden, system or directory
Const attrib_mask = fbNormal Or fbHidden Or fbSystem Or fbDirectory ' = &h37

Dim As UInteger out_attr '' unsigned integer to hold retrieved attributes

Dim As String fname '' file/directory name returned with
Dim As Integer filecount, dircount

fname = Dir("*.*", attrib_mask, out_attr) '' Get first file name/attributes, according to supplied file spec and attribute mask

Print "File listing in " & CurDir & ":"

Do Until Len(fname) = 0 '' loop until Dir returns empty string

	If (fname <> ".") And (fname <> "..") Then '' ignore current and parent directory entries

	    Print fname,

	    If out_attr And fbDirectory Then
	        Print "- directory";
	        dircount += 1
	    Else
	        Print "- file";
	        filecount += 1
	    End If
	    If out_attr And fbReadOnly Then Print ", read-only";
	    If out_attr And fbHidden Then Print ", hidden";
	    If out_attr And fbSystem Then Print ", system";
	    If out_attr And fbArchive Then Print ", archived";
	    Print

	End If

	fname = Dir(out_attr) '' find next name/attributes

Loop

Print
Print "Found " & filecount & " files and " & dircount & " subdirs"
%%

{{fbdoc item="target"}}
	- Linux requiert que la casse de ##//filename//## corresponde au nom r&eacute;el du fichier. Windows et DOS ne sont pas sensibles &agrave; la casse.
	- Les s&eacute;parateurs de chemin, sous Linux, sont des barres obliques ##/##. Windows utilise des anti-slashs ##\## mais il permet de barres obliques.  DOS utilise des anti-slashs.
	- Sous DOS, une valeur du masque "attrib" de ##&h37## (##&h3F## fonctionne habituellement aussi, mais ##&h37## est plus s&ucirc;r) retourne tous les fichiers et r&eacute;pertoires, incluant "." and "..", mais pas de "Volume": la valeur ##8## retourne le "Volume", m&ecirc;me si le r&eacute;pertoire courant n'est pas dans le r&eacute;pertoire principal.

{{fbdoc item="lang"}}
	- Non disponible dans le dialecte //[[CompilerOptlang -lang qb]]// sauf si r&eacute;f&eacute;renc&eacute; avec l'alias ##**__Dir**##.

{{fbdoc item="diff"}}
	- N'existe pas en QBasic mais en Basic.  Le param&egrave;tre ##//out_attrib//## est nouveau pour FreeBASIC.

{{fbdoc item="see"}}
	- ##[[KeyPgOpen Open]]##
	- ##[[KeyPgCurdir Curdir]]##
	- ##[[KeyPgMkdir Mkdir]]##
	- ##[[KeyPgRmdir Rmdir]]##

{{fbdoc item="back" value="CatPgOpsys|Operating System Functions"}}