{{fbdoc item="title" value="RETURN"}}----
D&eacute;claration de contr&ocirc;le de flux pour revenir d'une proc&eacute;dure ou d'un ##[[KeyPgGosub GoSub]]##.

{{fbdoc item="syntax"}}##
	**Return** [ //expression// ]
//ou//
	**Return** [ //label// ]

##
{{fbdoc item="desc"}}
	##**Return**## est utilis&eacute; pour revenir d'une proc&eacute;dure ou d'un ##[[KeyPgGosub GoSub]]##.

	Comme ##[[KeyPgReturn Return]]## signifie retour-d'un-gosub ou retour-d'une-proc&eacute;dure, ##[[KeyPgOptiongosub Option Gosub]]## et ##[[KeyPgOptionnogosub Option Nogosub]]## peuvent &ecirc;tre utilis&eacute;s pour activer et d&eacute;sactiver le support de ##[[KeyPgGosub GoSub]]##.  Quand le support de ##[[KeyPgGosub GoSub]]## est d&eacute;sactiv&eacute;, ##[[KeyPgReturn Return]]## est alors reconnu comme retour-d'une-proc&eacute;dure.  Quand le support de ##[[KeyPgGosub GoSub]]## est activ&eacute;, ##[[KeyPgReturn Return]]## est alors reconnu comme retour-d'un-gosub.

	##**Return**## (d'une proc&eacute;dure) est utilis&eacute; dans une proc&eacute;dure pour sortir de la proc&eacute;dure peut-&ecirc;tre avec une valeur de retour. Un ##[[KeyPgSub Sub]]## ne peut pas sp&eacute;cifier une valeur de retour.  Dans une ##[[KeyPgFunction Function]]##, ##**Return**## doit pr&eacute;ciser sa valeur de retour.  ##**Return** //expression//## est &agrave; peu pr&egrave;s &eacute;quivalent &agrave; ##Function = //expression// : [[KeyPgExit Exit]] Function##.

	##**Return**## (d'un gosub) est utilis&eacute; pour redonner le contr&ocirc;le &agrave; l'instruction qui suit imm&eacute;diatement le dernier appel &agrave; ##[[KeyPgGosub GoSub]]##. Lorsqu'il est utilis&eacute; en combinaison avec ##[[KeyPgGosub GoSub]]##, aucune valeur de retour ne peut &ecirc;tre sp&eacute;cifi&eacute;e.  Si l'option ##//label//## est sp&eacute;cifi&eacute;e, l'ex&eacute;cution se poursuit &agrave; l'&eacute;tiquette sp&eacute;cifi&eacute;e.  Si aucun ##[[KeyPgGosub GoSub]]## n'avait &eacute;t&eacute; ex&eacute;cut&eacute;, une erreur d'ex&eacute;cution est g&eacute;n&eacute;r&eacute;e et l'ex&eacute;cution continue imm&eacute;diatement apr&egrave;s ##**Return**##.

	Un ##[[KeyPgGosub GoSub]]## devrait toujours avoir un ##**Return**## correspondant.  Toutefois, si ##**Return**## (depuis un gosub) est utilis&eacute; lorsqu'aucun ##[[KeyPgGosub GoSub]]## n'a &eacute;t&eacute; ex&eacute;cut&eacute;, une erreur d'ex&eacute;cution est g&eacute;n&eacute;r&eacute;e.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/control/return.bas"}}%%(freebasic)
'' GOSUB & RETURN example, compile with "-lang qb" or use "$lang" as below

'$lang: "qb"

Print "Let's Gosub!"
GoSub MyGosub
Print "Back from Gosub!"
Sleep
End

MyGosub:
Print "In Gosub!"
Return
%%

{{fbdoc item="filename" value="examples/manual/control/return2.bas"}}%%(freebasic)
'' Return from function

type rational              '' simple rational number type
	numerator as integer
	denominator as integer
end type

'' multiplies two rational types
function rational_multiply( r1 as rational, r2 as rational ) as rational

	dim r as rational
	'' multiply the divisors ...
	r.numerator   = r1.numerator   * r2.numerator
	r.denominator = r1.denominator * r2.denominator

	'' ... and return the result
	return r

end function

dim as rational r1 = ( 6, 105 )   '' define some rationals r1 and r2
dim as rational r2 = ( 70, 4 )
dim as rational r3

r3 = rational_multiply( r1, r2 )  '' multiply and store the result in r3

'' display the expression
print r1.numerator & "/" & r1.denominator; " * ";
print r2.numerator & "/" & r2.denominator; " = ";
print r3.numerator & "/" & r3.denominator%%

{{fbdoc item="lang"}}
	- Dans le dialecte //[[CompilerOptlang -lang fb]]// ##**Return**## signifie toujours retour-d'une-proc&eacute;dure.
	- Dans le dialecte //[[CompilerOptlang -lang qb]]//, ##**Return**## signifie retour-d'un-gosub par d&eacute;faut sauf si cela a &eacute;t&eacute; chang&eacute; avec ##[[KeyPgOptionnogosub Option Nogosub]]##, auquel cas le compilateur reconna&icirc;tra ##**Return**## comme retour-d'une-proc&eacute;dure.
	- Dans le dialecte //[[CompilerOptlang -lang fblite]]//, ##**Return**## signifie retour-d'une-proc&eacute;dure par d&eacute;faut sauf si cela a &eacute;t&eacute; chang&eacute; avec ##[[KeyPgOptiongosub Option Gosub]]##, auquel cas le compilateur reconna&icirc;tra ##**Return**## comme retour-d'un-gosub.

{{fbdoc item="diff"}}
	- Aucune en utilisant le dialecte //[[CompilerOptlang -lang qb]]//.

{{fbdoc item="see"}}
	- ##[[KeyPgSub Sub]]##
	- ##[[KeyPgFunction Function]]##
	- ##[[KeyPgGosub Gosub]]##
	- ##[[KeyPgOptiongosub Option Gosub]]##
	- ##[[KeyPgOptionnogosub Option Nogosub]]##

{{fbdoc item="back" value="CatPgControlFlow|Control Flow"}}
