{{fbdoc item="title" value="RETURN"}}----
Déclaration de contrôle de flux pour revenir d'une procédure ou d'un ##[[KeyPgGosub Gosub]]##.

{{fbdoc item="syntax"}}##
	**Return** [ //expression// ]
//ou//
	**Return** [ //label// ]

##
{{fbdoc item="desc"}}
	##**Return**## est utilisé pour revenir d'une procédure ou d'un ##[[KeyPgGosub Gosub]]##.

	Comme ##[[KeyPgReturn Return]]## signifie retour-d'un-gosub ou retour-d'une-procédure, ##[[KeyPgOptiongosub Option Gosub]]## et ##[[KeyPgOptionnogosub Option Nogosub]]## peuvent être utilisés pour activer et désactiver le support de ##[[KeyPgGosub Gosub]]##.  Quand le support de ##[[KeyPgGosub Gosub]]## est désactivé, ##[[KeyPgReturn Return]]## est alors reconnu comme retour-d'une-procédure.  Quand le support de ##[[KeyPgGosub Gosub]]## est activé, ##[[KeyPgReturn Return]]## est alors reconnu comme retour-d'un-gosub.

	##**Return**## (d'une procédure) est utilisé dans une procédure pour sortir de la procédure peut-être avec une valeur de retour. Un ##[[KeyPgSub Sub]]## ne peut pas spécifier une valeur de retour.  Dans une ##[[KeyPgFunction Function]]##, ##**Return**## doit préciser sa valeur de retour.  ##**Return** //expression//## est à peu près équivalent à ##Function = //expression// : [[KeyPgExit Exit]] Function##.

	##**Return**## (d'un gosub) est utilisé pour redonner le contrôle à l'instruction qui suit immédiatement le dernier appel à ##[[KeyPgGosub Gosub]]##. Lorsqu'il est utilisé en combinaison avec ##[[KeyPgGosub Gosub]]##, aucune valeur de retour ne peut être spécifiée.  Si l'option ##//label//## est spécifiée, l'exécution se poursuit à l'étiquette spécifiée.  Si aucun ##[[KeyPgGosub Gosub]]## n'avait été exécuté, une erreur d'exécution est générée et l'exécution continue immédiatement après ##**Return**##.

	Un ##[[KeyPgGosub Gosub]]## devrait toujours avoir un ##**Return**## correspondant.  Toutefois, si ##**Return**## (depuis un gosub) est utilisé lorsqu'aucun ##[[KeyPgGosub Gosub]]## n'a été exécuté, une erreur d'exécution est générée.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/control/return.bas"}}%%(freebasic)
'' GOSUB & RETURN example, compile with "-lang qb" or use "$lang" as below

'$lang: "qb"

Print "Let's Gosub!"
GoSub MyGosub
Print "Back from Gosub!"
Sleep
End

MyGosub:
Print "In Gosub!"
Return
%%

{{fbdoc item="filename" value="examples/manual/control/return2.bas"}}%%(freebasic)
'' Return from function

type rational              '' simple rational number type
	numerator as integer
	denominator as integer
end type

'' multiplies two rational types
function rational_multiply( r1 as rational, r2 as rational ) as rational

	dim r as rational
	'' multiply the divisors ...
	r.numerator   = r1.numerator   * r2.numerator
	r.denominator = r1.denominator * r2.denominator

	'' ... and return the result
	return r

end function

dim as rational r1 = ( 6, 105 )   '' define some rationals r1 and r2
dim as rational r2 = ( 70, 4 )
dim as rational r3

r3 = rational_multiply( r1, r2 )  '' multiply and store the result in r3

'' display the expression
print r1.numerator & "/" & r1.denominator; " * ";
print r2.numerator & "/" & r2.denominator; " = ";
print r3.numerator & "/" & r3.denominator%%

{{fbdoc item="lang"}}
	- Dans le dialecte //[[CompilerOptlang -lang fb]]// ##**Return**## signifie toujours retour-d'une-procédure.
	- Dans le dialecte //[[CompilerOptlang -lang qb]]//, ##**Return**## signifie retour-d'un-gosub par défaut sauf si cela a été changé avec ##[[KeyPgOptionnogosub Option Nogosub]]##, auquel cas le compilateur reconnaîtra ##**Return**## comme retour-d'une-procédure.
	- Dans le dialecte //[[CompilerOptlang -lang fblite]]//, ##**Return**## signifie retour-d'une-procédure par défaut sauf si cela a été changé avec ##[[KeyPgOptiongosub Option Gosub]]##, auquel cas le compilateur reconnaîtra ##**Return**## comme retour-d'un-gosub.

{{fbdoc item="diff"}}
	- Aucune en utilisant le dialecte //[[CompilerOptlang -lang qb]]//.

{{fbdoc item="see"}}
	- ##[[KeyPgSub Sub]]##
	- ##[[KeyPgFunction Function]]##
	- ##[[KeyPgGosub Gosub]]##
	- ##[[KeyPgOptiongosub Option Gosub]]##
	- ##[[KeyPgOptionnogosub Option Nogosub]]##

{{fbdoc item="back" value="CatPgControlFlow|Control Flow"}}
