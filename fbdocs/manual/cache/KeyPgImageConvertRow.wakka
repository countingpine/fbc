{{fbdoc item="title" value="IMAGECONVERTROW"}}----
Convertit une ligne de données image dans une autre profondeur de couleur

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgSub sub]] **""ImageConvertRow""** ( [[KeyPgByval byval]] //src// [[KeyPgAs as]] [[KeyPgAny any]]  [[KeyPgPtr ptr]], [[KeyPgByval byval]] //src_bpp// [[KeyPgAs as]] [[KeyPgInteger integer]], [[KeyPgByval byval]] //dst// [[KeyPgAs as]] [[KeyPgAny any]]  [[KeyPgPtr ptr]], [[KeyPgByval byval]] //dst_bpp// [[KeyPgAs as]] [[KeyPgInteger integer]], [[KeyPgByval byval]] //width// [[KeyPgAs as]] [[KeyPgInteger integer]], [[KeyPgByval byval]] //isrgb// [[KeyPgAs as]] [[KeyPgInteger integer]] = 1 )
##
{{fbdoc item="usage"}}##
	**""ImageConvertRow""**( //src//, //src_bpp//, //dst//, //dst_bpp//, //width// [, //isrgb// ] )
##
{{fbdoc item="param"}}
	##//src//##
		L'adresse de départ de la ligne source. Lasource est soit une image pleine couleur (full-color) avec uneprofondeur de couleur de 24 ou 32 bits par pixel, soit une image avecpalette d'une profondeur de 1-8 bits par pixel. Convertir une imageavec palette ne fonctionnera correctement que si vous êtes dans un modeécran utilisant la palette correcte pour l'image lors de la conversion.
	##//src_bpp//##
		Le nombre de bits par pixel dans la ligne source.  1-8, 24 et 32.
	##//dst//##
		L'adresse du début de la ligne destination.L'image peut être une image pleine couleur de 16 ou 32 bits par pixel.Si la source est une image avec palette, la destination peut aussi êtreune image avec palette de 1 à 8 bits par pixel.
	##//dst_bpp//##
		Le nombre de bits par pixel dans la ligne destination.  Valeurs valides: 1-8, 16 et 32.
	##//width//##
		La longueur de la ligne en pixels.
	##//isrgb//##
		Une valeur de zéro indique que les canaux Rougeet Bleu sont l'autre voie dans l'image source. Utilisez cet indicateursi vous voulez que les canaux Rouge et Bleu doivent être permutéspendant la conversion.

{{fbdoc item="desc"}}
	Copie une ligne d'une image d'uneposition mémoire vers une autre, convertit l'information couleur dechaque pixel pour correspondre à l'image destination.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/imageconvertrow.bas"}}%%(freebasic)
#include "fbgfx.bi"
#if __fb_lang__ = "fb"
Using FB
#endif

Const As Integer w = 64, h = 64
Dim As IMAGE Ptr img8, img32
Dim As Integer x, y


'' Cree une image 32-bit, taille w*h:
Screenres 1, 1, 32, , GFX_NULL
img32 = ImageCreate(w, h)

If img32 = 0 Then Print "Imagecreate failed on img32!": Sleep: End


'' Cree une image 8-bit, taille w*h:
Screenres 1, 1, 8, , GFX_NULL
img8 = ImageCreate(w, h)

If img8 = 0 Then Print "Imagecreate failed on img8!": Sleep: End


'' Remplit image 8-bit avec un motif
for y = 0 to h - 1
	for x = 0 to w - 1
	    pset img8, (x, y), 56 + (x + y) mod 24
	next x
next y


'' Ouvre une fenetre graphique en mode 8-bit, et PUT l' image dans celle-ci:
Screenres 320, 200, 8
Windowtitle "8-bit color mode"
Put (10, 10), img8

Sleep


'' Copie les donnees image dans une image 32-bit
Dim As Byte Ptr p8, p32
Dim As Integer pitch8, pitch32

#ifndef ImageInfo '' older versions of FB don't have the ImageInfo feature
#define GETPITCH(img_) iif(img_->type=PUT_HEADER_NEW,img_->pitch,img_->old.width*img_->old.bpp)
#define GETP(img_) CPtr(Byte Ptr,img_)+iif(img_->type=PUT_HEADER_NEW,sizeof(PUT_HEADER),sizeof(_OLD_HEADER))
pitch8 = GETPITCH(img8): p8 = GETP(img8)
pitch32 = GETPITCH(img32): p32 = GETP(img32)
#else
ImageInfo( img8,  , , , pitch8,  p8  )
ImageInfo( img32, , , , pitch32, p32 )
#endif

For y = 0 To h - 1
	ImageConvertRow(@p8 [ y * pitch8 ],  8, _
	                @p32[ y * pitch32], 32, _
	                w)
Next y


'' Ouvre une fenetre graphique en mode 32-bit et PUT l' image sur celle-ci:
Screenres 320, 200, 32
Windowtitle "32-bit color mode"
Put (10, 10), img32

Sleep


'' Libere la memoire des images:
ImageDestroy img8
ImageDestroy img32
%%

{{fbdoc item="lang"}}
	Non disponible dans le dialecte //[[CompilerOptlang -lang qb]]// sauf si référencé avec l'alias ##**__ImageConvertRow**##.

{{fbdoc item="diff"}}
	- Nouveau pour ""FreeBASIC""

{{fbdoc item="see"}}
	- ##[[KeyPgScreenres ScreenRes]]##
	- ##[[KeyPgGetgraphics Get (Graphique)]]##
	- ##[[KeyPgPutgraphics Put (Graphique)]]##
	- ##[[KeyPgImagecreate ImageCreate]]##
	- ##[[KeyPgImageDestroy ImageDestroy]]##
	- ##[[KeyPgImageInfo ImageInfo]]##

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}

